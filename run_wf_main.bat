@echo off
setlocal EnableExtensions
cd /d "%~dp0"

REM Corrida headless de wf_main_win (Programador de tareas Windows).
REM Override: set HOP_RUN=D:\ruta\a\hop\hop-run.bat

REM --- Log por fecha ---
set "LOGDIR=%~dp0logs"
if not exist "%LOGDIR%" mkdir "%LOGDIR%"
for /f %%a in ('powershell -NoProfile -Command "Get-Date -Format yyyyMMdd"') do set "STAMP=%%a"
set "LOGFILE=%LOGDIR%\wf_main_%STAMP%.log"
if exist "%LOGFILE%" del "%LOGFILE%" >nul 2>&1

call :log Inicio: %date% %time%

REM Levantar H2 como tarea independiente (desacoplado de la consola de Hop,
REM evita el cuelgue por Ctrl-C del batch). reset_and_create.bat solo aplica
REM DDL; no lanza ni detiene el server H2 (es idempotente).
call "%~dp0h2\scripts\start_h2_svc.bat" >> "%LOGFILE%" 2>&1

REM Resolver hop-run.bat: override HOP_RUN > D:\Eder\hop > %USERPROFILE%\apps\hop > PATH
if not defined HOP_RUN (
  if exist "D:\Eder\hop\hop-run.bat" (
    set "HOP_RUN=D:\Eder\hop\hop-run.bat"
  ) else if exist "%USERPROFILE%\apps\hop\hop-run.bat" (
    set "HOP_RUN=%USERPROFILE%\apps\hop\hop-run.bat"
  ) else if exist "%USERPROFILE%\apps\hop\hop-run.cmd" (
    set "HOP_RUN=%USERPROFILE%\apps\hop\hop-run.cmd"
  ) else (
    set "HOP_RUN=hop-run"
  )
)

set "PROJECT_HOME=%CD%"
set "PROJECT_NAME=multa_informes_etl"
set "WF=%PROJECT_HOME%\workflows\wf_main_win.hwf"

call :log HOP_RUN=%HOP_RUN%
call :log PROJECT_HOME=%PROJECT_HOME%
call :log Ejecutando %WF%

if /I "%HOP_RUN%"=="hop-run" (
  where hop-run >nul 2>&1
  if errorlevel 1 (
    call :log ERROR: No se encuentra hop-run en PATH. Define HOP_RUN.
    exit /b 1
  )
) else if not exist "%HOP_RUN%" (
  call :log ERROR: No se encuentra %HOP_RUN%. Define HOP_RUN o instala Hop.
  exit /b 1
)

call "%HOP_RUN%" ^
  --project=%PROJECT_NAME% ^
  --file="%WF%" ^
  --level=Basic ^
  --runconfig=local >> "%LOGFILE%" 2>&1

set "RC=%ERRORLEVEL%"
call :log Fin wf_main_win exit=%RC%
exit /b %RC%

:log
echo [%date% %time%] %*>> "%LOGFILE%"
exit /b 0